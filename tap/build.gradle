import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'war'

    // IntelliJ IDEA plugin here to allow integration tests to be properly loaded.
    id 'idea'
}

repositories {
    mavenCentral()
    mavenLocal()
}

apply from: '../properties.gradle'

war {
    eachFile {
        if (it.name == 'capabilities.xml' || it.name == 'service.json') {
            filter(ReplaceTokens, tokens: [app_name: 'tap', version: version])
        }
    }
    archiveFileName = 'tap.war'

    manifest {
        attributes "Implementation-Version": version
        attributes "Implementation-Title": "TAP"
    }

    // Include the swagger-ui so that /tap provides the TAP API documentation
    from('../') {
        include 'swagger-ui/'
    }
}

apply from: '../int_test.gradle'

dependencies {
    implementation 'com.oracle.database.jdbc:ojdbc10:19.16.0.0'
    implementation 'org.opencadc:alma-lib:' + version
    implementation 'org.opencadc:cadc-adql:[1.1.11,1.2.0)'
    implementation 'org.opencadc:cadc-log:[1.1.7,1.2.0)'
    implementation 'org.opencadc:cadc-rest:[1.3.11,1.4.0)'
    implementation 'org.opencadc:cadc-tap-server:[1.1.31,1.2.0)'
    implementation 'org.opencadc:cadc-tap-server-oracle:[1.2.11,1.3.0)'
    implementation 'org.opencadc:cadc-tap-tmp:[1.1.0,1.2.0)'
    implementation 'org.opencadc:cadc-util:[1.6.1,1.7.0)'
    implementation 'org.opencadc:cadc-uws:[1.0.5,1.1.0)'
    implementation 'org.opencadc:cadc-uws-server:[1.2.14,1.3.0)'
    implementation 'org.opencadc:cadc-vosi:[1.4.9,2.0)'

    testImplementation 'junit:junit:[4.13,5.0)'
    testImplementation 'org.mockito:mockito-core:[3.9,4.0)'
    testImplementation 'org.opencadc:cadc-test-uws:[1.2.2,)'
    testImplementation 'org.opencadc:cadc-test-vosi:[1.0.2,)'
    testImplementation 'org.opencadc:cadc-test-tap:[1.1,)'
}

tasks.withType(Test) {
    // Reset the report destinations so that intTests go to their own page
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)

    // Assign all Java system properties from the command line to the tests
    systemProperties System.properties as Map<String, ?>
}
